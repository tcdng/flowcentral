// UPL template for workflow step edit panel
// Copyright 2021-2022 FlowCentral Technologies Limited.
// 
// Licensed under the Apache License, Version 2.0 (the "License"); you may not
// use this file except in compliance with the License. You may obtain a copy of
// the License at
// 
// http://www.apache.org/licenses/LICENSE-2.0
// 
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
// WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
// License for the specific language governing permissions and limitations under
// the License.

!fc-wfstepeditpanel
    dialogBodyComponents:$c{formPanel}
    dialogActionLayout:$d{!ui-horizontal style:$s{width:100%;}}
    dialogActionComponents:$c{dialogActionPanel}

//Form   
!ui-formpanel:formPanel
	layout:$d{!ui-vertical style:$s{table-layout:fixed;}}
    formCaptionSuffix:$s{:} formBinding:$g{dialogFormBinding} formColumns:2
	formSection:$d{!ui-section components:$c{frmType frmName frmDesc frmLabel frmAppletName frmBinaryConditionName frmReadOnlyConditionName frmPolicy frmRule
		frmActionType frmNextStepName frmAltNextStepName frmPriority frmCriticalMinutes frmExpiryMinutes frmAudit frmBranchOnly frmIncludeForwarder frmForwarderPreferred}}

!ui-name:frmName styleClass:$e{fc-formfield} caption:$m{wfstepeditpanel.name} size:32 binding:name required:true
!ui-text:frmDesc styleClass:$e{fc-formfield} caption:$m{wfstepeditpanel.description} size:32 binding:description required:true focus:true
!ui-text:frmLabel styleClass:$e{fc-formfield} caption:$m{wfstepeditpanel.label} binding:label required:true
!ui-select:frmAppletName styleClass:$e{fc-formfield} caption:$m{wfstepeditpanel.applet} listParams:$l{entityName}
	list:$s{studioentityappletlist} blankOption:$s{} binding:appletName required:true
!ui-text:frmNextStepName styleClass:$e{fc-formfield} caption:$m{wfstepeditpanel.nextstepname} size:32 binding:nextStepName
!ui-text:frmAltNextStepName styleClass:$e{fc-formfield} caption:$m{wfstepeditpanel.altnextstepname} size:32 binding:altNextStepName
!ui-integer:frmCriticalMinutes styleClass:$e{fc-formfield} caption:$m{wfstepeditpanel.criticalminutes} precision:5 binding:criticalMinutes
!ui-integer:frmExpiryMinutes styleClass:$e{fc-formfield} caption:$m{wfstepeditpanel.expiryminutes} precision:5 binding:expiryMinutes
!ui-checkbox:frmAudit caption:$m{wfstepeditpanel.audit} binding:audit
!ui-checkbox:frmBranchOnly caption:$m{wfstepeditpanel.branchonly} binding:branchOnly
!ui-checkbox:frmIncludeForwarder caption:$m{wfstepeditpanel.includeforwarder} binding:includeForwarder
!ui-checkbox:frmForwarderPreferred caption:$m{wfstepeditpanel.forwarderpreferred} binding:forwarderPreffered
!ui-select:frmType styleClass:$e{fc-formfield} caption:$m{wfstepeditpanel.type}
	list:$s{wfsteptypelist} blankOption:$s{} binding:type required:true
!ui-select:frmPriority styleClass:$e{fc-formfield} caption:$m{wfstepeditpanel.priority}
	list:$s{wfstepprioritylist} blankOption:$s{} binding:priority required:true
!ui-select:frmBinaryConditionName styleClass:$e{fc-formfield} caption:$m{wfstepeditpanel.binarycondition}
	list:$s{studioworkflowfilterlist} listParams:$l{workflowId} blankOption:$s{}
	binding:binaryConditionName required:true
!ui-select:frmReadOnlyConditionName styleClass:$e{fc-formfield} caption:$m{wfstepeditpanel.readonlycondition}
	list:$s{studioworkflowfilterlist} listParams:$l{workflowId} blankOption:$s{}
	binding:readOnlyConditionName
!ui-select:frmPolicy styleClass:$e{fc-formfield} caption:$m{wfstepeditpanel.policy}
	list:$s{wfsteppolicylist} listParams:$l{type} blankOption:$s{}
	binding:policy required:true
	eventHandler:$d{!ui-event event:onchange action:$c{switchAct}}
!ui-select:frmRule styleClass:$e{fc-formfield} caption:$m{wfstepeditpanel.rule}
	list:$s{rulelist} listParams:$l{policy} blankOption:$s{}
	binding:rule
!ui-select:frmActionType styleClass:$e{fc-formfield} caption:$m{wfstepeditpanel.recordactiontype}
	list:$s{recordactiontypelist} blankOption:$s{} binding:recordActionType required:true
	

// Action buttons
!ui-panel:dialogActionPanel layout:$d{!ui-horizontal} components:$c{applyBtn cancelBtn}
!ui-button:applyBtn caption:$m{button.apply} styleClass:$e{fc-bluebutton}
	eventHandler:$d{!ui-event event:onclick action:$c{applyAct}}
!ui-button:cancelBtn caption:$m{button.cancel}
    eventHandler:$d{!ui-event event:onclick action:$c{closeDialogAct}}

//Actions
!ui-postcommand:switchAct components:$c{formPanel}  refresh:$c{formPanel}
!ui-postcommand:applyAct command:apply components:$c{formPanel}
    validations:$c{formValidation} refresh:$f{refreshOnApply}


// Validations
!ui-validation:formValidation components:$c{formPanel}
	